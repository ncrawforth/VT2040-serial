.program serial_rx
baudloop:               ; Wait for a clean 115200 baud start bit:
  wait 1 pin 0          ;   Wait for rx pin to go high
  wait 0 pin 0 [9]      ;   Wait for rx pin to go low, then wait 9 cycles
  jmp pin byteloop      ;   If rx pin is high, start receiving
  jmp baudloop          ;   Otherwise serial is still transmitting at 74880 baud
byteloop:               ; Receive data at 115200 baud:
  set x, 7              ;   Set bit counter
  bitloop:              ;   Receive 8 bits:
    in pins, 1          ;     Receive bit
    jmp x-- bitloop [6] ;     Wait for next bit
  wait 0 pin 0 [11]     ;   Wait for start bit, then wait until middle of next bit
  jmp byteloop

.program serial_tx
.side_set 1 opt
pull side 1 [7]         ; Pull data from fifo, and send a stop bit
set x, 7 side 0 [7]     ; Set bit counter, and send a start bit
bitloop:                ; Send 8 bits
  out pins, 1
  jmp x-- bitloop [6]
